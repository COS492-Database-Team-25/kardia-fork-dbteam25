$Version=2$
cr_batches "widget/component-decl"
    {
    width=780;
    height=542;

    ledger "widget/parameter" { type=string; default=null; deploy_to_client=yes; }
    periods_osrc "widget/parameter" { type=object; default=null; }
    receipts_rpt "widget/parameter" { type=string; default=runserver(isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows where :a_ledger_number = :this:ledger and :a_config_name = 'RcptReport'), "/apps/kardia/modules/rcpt/receipt_batch.rpt")); deploy_to_client=yes; }
    ncreceipts_rpt "widget/parameter" { type=string; default=runserver(isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows where :a_ledger_number = :this:ledger and :a_config_name = 'RcptNCReport'), "/apps/kardia/modules/rcpt/receipt_batch.rpt")); deploy_to_client=yes; }
    acks_rpt "widget/parameter" { type=string; default=runserver(isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows where :a_ledger_number = :this:ledger and :a_config_name = 'RcptAckReport'), "/apps/kardia/modules/rcpt/acknowledgement_batch.rpt")); deploy_to_client=yes; }
    cash_cc "widget/parameter" { type=string; default=runserver( isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows where :a_config_name = 'RcptCashFund' and :a_ledger_number = :this:ledger), '')); deploy_to_client=yes; }
    cash_acct "widget/parameter" { type=string; default=runserver( isnull( (select :a_config_value from /apps/kardia/data/Kardia_DB/a_config/rows where :a_config_name = 'RcptCashAcct' and :a_ledger_number = :this:ledger), '')); deploy_to_client=yes; }
    gotobatch "widget/parameter" { type=integer; deploy_to_client=yes; }

    post_cr "widget/component"
	{
	path = "/apps/kardia/modules/rcpt/post_cr_batch.cmp";

	post_cn "widget/connector" { event=Posted; target=rcpt_osrc; action=Refresh; }
	glpost_cn "widget/connector" { event=GLPosted; target=rcpt_osrc; action=Refresh; }
	unpost_cn "widget/connector" { event=Unposted; target=rcpt_osrc; action=Refresh; }
	glunpost_cn "widget/connector" { event=GLUnposted; target=rcpt_osrc; action=Refresh; }
	setdate_cn1 "widget/connector" { event=SetSentDateComplete; target=cr_batches; action=Launch; Source=runclient(:receipts_rpt:value); Width=800; Height=600; ledger=runclient(:rcpt_osrc:a_ledger_number); batch_number=runclient(:rcpt_osrc:a_batch_number); show_background=runclient(:show_bg:value); types=runclient(:print_which_type:value); }
	setdate_cn2 "widget/connector" { event=SetSentDateComplete; target=rcpt_osrc; action=Refresh; }
	setackdate_cn1 "widget/connector" { event=SetAckSentDateComplete; target=cr_batches; action=Launch; Source=runclient(:acks_rpt:value); Width=800; Height=600; ledger=runclient(:rcpt_osrc:a_ledger_number); batch_number=runclient(:rcpt_osrc:a_batch_number); show_background=runclient(:show_bg:value); types=runclient(:print_which_type:value); }
	setackdate_cn2 "widget/connector" { event=SetAckSentDateComplete; target=rcpt_osrc; action=Refresh; }
	setncdate_cn1 "widget/connector" { event=SetNCSentDateComplete; target=cr_batches; action=Launch; Source=runclient(:ncreceipts_rpt:value); Width=800; Height=600; ledger=runclient(:rcpt_osrc:a_ledger_number); batch_number=runclient(:rcpt_osrc:a_batch_number); show_background=runclient(:show_bg:value); types=runclient(:print_which_type:value); }
	setncdate_cn2 "widget/connector" { event=SetNCSentDateComplete; target=rcpt_osrc; action=Refresh; }
	}

    refresh_timer "widget/timer"
	{
	msec=300000; // 5min = 300sec = 300000msec
	auto_reset=1;
	auto_start=0;
	refresh_cn "widget/connector" 
	    {
	    event=Expire;
	    event_condition=runclient(:new_batch_form:form_mode == 'View' and :new_batch_window:is_visible = 0 and :post_to_gl_window:is_visible = 0 and :print_rcpt_window:is_visible = 0);
	    target=rcpt_osrc;
	    action=Refresh;
	    }
	}

    rcpt_osrc "widget/osrc"
	{
	sql = runserver("
		SELECT
			:b:a_ledger_number,
			:b:a_batch_number, 
			:b:a_batch_desc, 
			:b:a_default_effective_date,
			date_txt = substring(convert(string,:a_default_effective_date),1,11),
			:b:s_date_created,
			:b:s_created_by,
			:b:s_date_modified,
			:b:s_modified_by,
			:b:a_period,
			amt = isnull((select sum(:a_amount) from /apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows cr where :cr:a_batch_number = :b:a_batch_number and :cr:a_ledger_number = :b:a_ledger_number), $0),
			rrec = isnull((select 1 from /apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows cr where :cr:a_batch_number = :b:a_batch_number and :cr:a_ledger_number = :b:a_ledger_number limit 1),0),
			rpost = isnull((select 1 from /apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows cr where :cr:a_batch_number = :b:a_batch_number and :cr:a_ledger_number = :b:a_ledger_number and :cr:a_posted = 1 limit 1),0),
			print = isnull((select 1 from /apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows cr where :cr:a_batch_number = :b:a_batch_number and :cr:a_ledger_number = :b:a_ledger_number and :cr:a_receipt_sent = 1 limit 1),0),
			glrec = isnull((select 1 from /apps/kardia/data/Kardia_DB/a_transaction/rows t where :t:a_batch_number = :b:a_batch_number and :t:a_ledger_number = :b:a_ledger_number limit 1),0)
		FROM 
			/apps/kardia/data/Kardia_DB/a_batch/rows b
		WHERE
			:b:a_origin = 'CR'
		ORDER BY
			:b:a_batch_number");
	baseobj = "/apps/kardia/data/Kardia_DB/a_batch/rows";
	replicasize=200;
	readahead=200;
	autoquery=never;
	indicates_activity = no;

	rcpt_endquery_cn "widget/connector" { target=refresh_timer; event=EndQuery; action=SetTimer; Time=300000; }

	period_sync "widget/rule"
	    {
	    ruletype = "osrc_relationship";
	    target = periods_osrc;
	    is_slave = yes;
	    key_1 = a_ledger_number;
	    target_key_1 = a_ledger_number;
	    key_2 = a_period;
	    target_key_2 = a_period;
	    autoquery = true;
	    }

	preselect_one_batch "widget/connector"
	    {
	    event=EndQuery;
	    event_condition=runclient(not (:gotobatch:value is null));
	    action=FindObject;
	    a_batch_number = runclient(:gotobatch:value);
	    }
	dont_preselect_batch_twice "widget/connector"
	    {
	    event=EndQuery;
	    event_condition=runclient(not (:gotobatch:value is null));
	    target=gotobatch;
	    action=SetValue;
	    Value=runclient(null);
	    }
			
	rcpt_form "widget/form" { }

	print_rcpt_window "widget/childwindow"
	    {
	    style=dialog;
	    titlebar=no;
	    toplevel=yes;
	    width=640; height=400;
	    x=70; y=70;
	    visible=no;
	    modal=yes;
	    border_radius=8;

	    print_vbox "widget/vbox"
		{
		width=618; height=378; x=10; y=10;
		spacing=10;

		print_lbl "widget/label" { height=24; font_size=16; value=runclient("Print Batch " + :rcpt_osrc:a_batch_number + " of Receipts"); align=center; style=bold; }

		sep_line4 "widget/pane" { height=2; fl_height=0; style=lowered; }

		print_which_type "widget/component"
		    {
		    path = "/sys/cmp/smart_field.cmp";
		    ctl_type = dropdown;
		    text = "Which Types?";
		    width = 350;
		    height = 20;
		    label_width = 140;
		    sql = "select 'All','_',0; select 'Only Immediate Receipts','I',1";

		    print_wt_hints "widget/hints" { style=notnull; default=runclient('I'); }

		    print_change_type "widget/connector"
			{
			event=DataChange;
			target=print_info_osrc;
			action=QueryParam;
			a_ledger_number = runclient(:rcpt_osrc:a_ledger_number);
			a_batch_number = runclient(:rcpt_osrc:a_batch_number);
			}
		    }

		show_bg "widget/component"
		    {
		    path="/sys/cmp/smart_field.cmp";
		    ctl_type=checkbox;
		    text="Print Background?"; 
		    label_width=140; 
		    height=20;
		    
		    show_bg_hints "widget/hints" { style=notnull; default=0; } 

		    set_zero_on_open "widget/connector" { source=print_rcpt_window; event=Open; action=SetValue; Value=0; }
		    }

		sent_date_pane "widget/pane"
		    {
		    width=350; height=20;
		    style=flat;
		    enabled = runclient(not :print_info_osrc:min_sent);
		    f_sent_date "widget/component" { x=0; y=0; width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=datetime; field=sent_date; text="Mailing Date:"; label_width=140; sd_hints "widget/hints" { default=runclient(getdate()); } }
		    }

		sep_line5 "widget/pane" { height=2; fl_height=0; style=lowered; }

		types_hbox "widget/hbox"
		    {
		    height=190;
		    spacing=10;

		    donor_receipts_vbox "widget/vbox"
			{
			width=200;
			spacing=5;

			donor_title "widget/label" { height=20; font_size=16; style=bold; text="Cash Receipts:"; align=center; }

			print_info_osrc "widget/osrc"
			    {
			    print_wt_param "widget/parameter" { type=string; default=runclient(:print_which_type:value); }
			    sql = "select
					min_rcptno = min(:g:a_receipt_number),
					max_rcptno = max(:g:a_receipt_number),
					num_rcpts = count(:g:a_receipt_number),
					min_sent = min(:g:a_receipt_sent)
				    from
					/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g,
					/apps/kardia/data/Kardia_DB/a_gift_payment_type/rows gt
				    where
					(:parameters:print_wt_param = '_' or :a_receipt_desired = :parameters:print_wt_param) and
					:gt:a_gift_payment_type = :g:a_gift_type and
					:gt:a_ledger_number = :g:a_ledger_number and
					:gt:a_is_cash = 1
				    ";
			    readahead = 2;
			    replicasize = 2;
			    autoquery = never;

			    do_print_info_qy "widget/connector"
				{
				source = print_rcpt_window;
				event = Open;
				action = QueryParam;
				a_ledger_number = runclient(:rcpt_osrc:a_ledger_number);
				a_batch_number = runclient(:rcpt_osrc:a_batch_number);
				}

			    print_info_form "widget/form"
				{
				f_min_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=min_rcptno; text="Starting Receipt:"; label_width=140; }
				f_max_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=max_rcptno; text="Ending Receipt:"; label_width=140; }
				f_num_rcpts "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=num_rcpts; text="Number of Receipts:"; label_width=140; }
				}
			    }
			sent_date_set "widget/connector"
			    {
			    source = print_rcpt_window;
			    event = Open;
			    target = f_sent_date;
			    action = SetValue;
			    Value = runclient(getdate());
			    }
			resend_label "widget/label"
			    {
			    height=50;
			    style=italic;
			    value = runclient(condition(:print_info_osrc:min_sent, "This batch of receipts has already been printed.  The 'receipt sent date' will not be updated for this batch.", ""));
			    }

			donor_ctls_hbox "widget/hbox"
			    {
			    height=24;
			    spacing=10;
			    align=center;

			    print_ok "widget/textbutton"
				{
				text = runclient(isnull(condition(:print_info_osrc:min_sent, "Reprint", "Print"), "(none)"));
				enabled=runclient(:print_info_osrc:min_sent is not null);
				print_ok_cn "widget/connector" 
				    {
				    event=Click;
				    target=post_cr;
				    action=SetSentDate; 
				    a_ledger_number=runclient(:cr_batches:ledger);
				    a_period=runclient(:periods_osrc:a_period);
				    a_batch_number=runclient(:rcpt_osrc:a_batch_number);
				    a_receipt_sent_date=runclient(:f_sent_date:value);
				    types = runclient(:print_which_type:value);
				    }
				//print_ok_cn2 "widget/connector" { event=Click; target=print_rcpt_window; action=Close; }
				}
			    }
			}

		    inkind_receipts_vbox "widget/vbox"
			{
			width=200;
			spacing=5;

			inkind_title "widget/label" { height=20; font_size=16; style=bold; text="Noncash Receipts:"; align=center; }

			nc_info_osrc "widget/osrc"
			    {
			    nc_wt_param "widget/parameter" { type=string; default=runclient(:print_which_type:value); }
			    sql = "select
					min_rcptno = min(:g:a_receipt_number),
					max_rcptno = max(:g:a_receipt_number),
					num_rcpts = count(:g:a_receipt_number),
					min_sent = min(:g:a_receipt_sent)
				    from
					/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g,
					/apps/kardia/data/Kardia_DB/a_gift_payment_type/rows gt
				    where
					(:parameters:nc_wt_param = '_' or :a_receipt_desired = :parameters:nc_wt_param) and
					:gt:a_gift_payment_type = :g:a_gift_type and
					:gt:a_ledger_number = :g:a_ledger_number and
					:gt:a_is_cash = 0
				    ";
			    readahead = 2;
			    replicasize = 2;
			    autoquery = never;

			    do_nc_info_qy "widget/connector"
				{
				source = print_rcpt_window;
				event = Open;
				action = QueryParam;
				a_ledger_number = runclient(:rcpt_osrc:a_ledger_number);
				a_batch_number = runclient(:rcpt_osrc:a_batch_number);
				}

			    nc_info_form "widget/form"
				{
				fn_min_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=min_rcptno; text="Starting Receipt:"; label_width=140; }
				fn_max_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=max_rcptno; text="Ending Receipt:"; label_width=140; }
				fn_num_rcpts "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=num_rcpts; text="Number of Receipts:"; label_width=140; }
				}
			    }
			nc_resend_label "widget/label"
			    {
			    height=50;
			    style=italic;
			    value = runclient(condition(:nc_info_osrc:min_sent, "This batch of receipts has already been printed.  The 'receipt sent date' will not be updated for this batch.", ""));
			    }
			nc_ctls_hbox "widget/hbox"
			    {
			    height=24;
			    spacing=10;
			    align=center;

			    ncprint_ok "widget/textbutton"
				{
				text = runclient(isnull(condition(:nc_info_osrc:min_sent, "Reprint", "Print"), "(none)"));
				enabled=runclient(:nc_info_osrc:min_sent is not null);
				ncprint_ok_cn "widget/connector" 
				    {
				    event=Click;
				    target=post_cr;
				    action=SetNCSentDate; 
				    a_ledger_number=runclient(:cr_batches:ledger);
				    a_period=runclient(:periods_osrc:a_period);
				    a_batch_number=runclient(:rcpt_osrc:a_batch_number);
				    a_receipt_sent_date=runclient(:f_sent_date:value);
				    types = runclient(:print_which_type:value);
				    }
				//print_ok_cn2 "widget/connector" { event=Click; target=print_rcpt_window; action=Close; }
				}
			    }
			}

		    ack_receipts_vbox "widget/vbox"
			{
			width=200;
			spacing=5;

			ack_title "widget/label" { height=20; font_size=16; style=bold; text="Acknowledgements:"; align=center; }

			ack_info_osrc "widget/osrc"
			    {
			    ack_wt_param "widget/parameter" { type=string; default=runclient(:print_which_type:value); }
			    sql = "select
					min_rcptno = min(:g:a_receipt_number),
					max_rcptno = max(:g:a_receipt_number),
					num_rcpts = count(:g:a_receipt_number),
					min_sent = min(:g:a_ack_receipt_sent)
				    from
					/apps/kardia/data/Kardia_DB/a_subtrx_gift_group/rows g,
					/apps/kardia/data/Kardia_DB/a_gift_payment_type/rows gt
				    where
					(:parameters:ack_wt_param = '_' or :a_ack_receipt_desired = :parameters:ack_wt_param) and
					:gt:a_gift_payment_type = :g:a_gift_type and
					:gt:a_ledger_number = :g:a_ledger_number and
					char_length(:g:p_ack_partner_id) > 1
				    ";
			    readahead = 2;
			    replicasize = 2;
			    autoquery = never;

			    do_ack_info_qy "widget/connector"
				{
				source = print_rcpt_window;
				event = Open;
				action = QueryParam;
				a_ledger_number = runclient(:rcpt_osrc:a_ledger_number);
				a_batch_number = runclient(:rcpt_osrc:a_batch_number);
				}

			    ack_info_form "widget/form"
				{
				fa_min_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=min_rcptno; text="Starting Receipt:"; label_width=140; }
				fa_max_rcptno "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=max_rcptno; text="Ending Receipt:"; label_width=140; }
				fa_num_rcpts "widget/component" { width=350; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=label; field=num_rcpts; text="Number of Receipts:"; label_width=140; }
				}
			    }
			ack_resend_label "widget/label"
			    {
			    height=50;
			    style=italic;
			    value = runclient(condition(:ack_info_osrc:min_sent, "This batch of acknowledgements has already been printed.  The 'sent date' will not be updated for this batch.", ""));
			    }
			ack_ctls_hbox "widget/hbox"
			    {
			    height=24;
			    spacing=10;
			    align=center;

			    ackprint_ok "widget/textbutton"
				{
				text = runclient(isnull(condition(:ack_info_osrc:min_sent, "Reprint", "Print"), "(none)"));
				enabled=runclient(:ack_info_osrc:min_sent is not null);
				ackprint_ok_cn "widget/connector" 
				    {
				    event=Click;
				    target=post_cr;
				    action=SetAckSentDate; 
				    a_ledger_number=runclient(:cr_batches:ledger);
				    a_period=runclient(:periods_osrc:a_period);
				    a_batch_number=runclient(:rcpt_osrc:a_batch_number);
				    a_ack_receipt_sent_date=runclient(:f_sent_date:value);
				    types = runclient(:print_which_type:value);
				    }
				//ack_ok_cn2 "widget/connector" { event=Click; target=print_rcpt_window; action=Close; }
				}
			    }
			}
		    }

		print_ctls_hbox "widget/hbox"
		    {
		    height=24;
		    spacing=10;
		    align=center;

		    print_cancel "widget/textbutton"
			{
			text = "Close";
			print_cancel_cn "widget/connector" { event=Click; target=print_rcpt_window; action=Close; }
			}
		    }
		}
	    }

	unpost_req_cmp "widget/component"
	    {
	    path = "/apps/kardia/modules/rcpt/unpost_req.cmp";
	    }

	unpostcr_win "widget/childwindow"
	    {
	    style=dialog;
	    titlebar=no;
	    toplevel=yes;
	    width=480; height=300;
	    x=150; y=140;
	    visible=no;
	    border_radius=8;

	    on_req_unpost_1 "widget/connector" { event=Open; target=gl_only_var; action=SetValue; Value=:gl_only; }
	    on_req_unpost_2 "widget/connector" { event=Open; target=unpost_from_gl; action=SetValue; Value=runclient(condition(:rcpt_form:glrec > 0, 1, 0)); }
	    on_req_unpost_3 "widget/connector" { event=Open; target=unpost_from_cr; action=SetValue; Value=runclient(condition(:gl_only, 0, 1)); }

	    gl_only_var "widget/variable" { type=integer; }

	    unpost_vbox "widget/vbox"
		{
		width=458; height=220; x=10; y=10;
		spacing=10;

		unpost_lbl "widget/label" { height=24; font_size=16; value=runclient("Request Unposting of Batch " + :rcpt_osrc:a_batch_number + ":"); align=center; style=bold; }
		unpost_sep "widget/pane" { height=2; style=lowered; }
		//sp4 "widget/autolayoutspacer" { height=8; }

		unpost_desc "widget/label" { height=50; style=italic; text="Unposting a batch is a privileged operation; you can request here that this batch be unposted, but another user who also has receipting and/or GL management privileges will need to approve the request"; }

		unpost_from_gl "widget/component" { x=10; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=checkboxleft; text="Unpost and Remove Batch from General Ledger"; unpost_gl_hints "widget/hints" { default=runclient(condition(:rcpt_form:glrec > 0, 1, 0)); style=notnull,readonly; } }
		unpost_from_cr "widget/component" { x=10; height=20; path="/sys/cmp/smart_field.cmp"; ctl_type=checkboxleft; text="Unpost Batch from Receipting Journal"; unpost_cr_hints "widget/hints" { default=runclient(condition(:gl_only_var:value, 0, 1)); style=notnull,readonly; } }
		unpost_comment "widget/component" { height=50; path="/sys/cmp/smart_field.cmp"; ctl_type=textarea; text="Comments:"; }

		sp4 "widget/autolayoutspacer" { height=2; }

		unpost_btns "widget/hbox"
		    {
		    height=24;
		    spacing=8;
		    align=center;

		    unpost_req "widget/textbutton"
			{
			height=24; width=150;
			text="Request Unposting";

			on_unpost_request_1 "widget/connector" { event=Click; target=unpost_req_cmp; action=RequestUnposting; ledger=runclient(:ledger:value); batch=runclient(:rcpt_osrc:a_batch_number); gl=runclient(:unpost_from_gl:value); cr=runclient(:unpost_from_cr:value); comment=runclient(:unpost_comment:value); }
			on_unpost_request_2 "widget/connector" { event=Click; target=unpostcr_win; action=Close; }
			}

		    unpost_cancel "widget/textbutton"
			{
			height=24; width=150;
			text="Cancel";

			on_unpost_cancel "widget/connector" { event=Click; target=unpostcr_win; action=Close; }
			}
		    }
		}
	    }

	post_to_gl_window "widget/childwindow"
	    {
	    style=dialog;
	    titlebar=no;
	    toplevel=yes;
	    width=480; height=300;
	    x=150; y=120;
	    visible=no;
	    border_radius=8;

	    gl_post_vbox "widget/vbox"
		{
		width=458; height=280; x=10; y=10;
		spacing=10;

		gl_post_lbl "widget/label" { height=24; font_size=16; value=runclient("Post Batch " + :rcpt_osrc:a_batch_number + " to General Ledger"); align=center; style=bold; }
		gl_post_sep "widget/pane" { height=2; style=lowered; }

		rev_osrc "widget/osrc"
		    {
		    readahead=20;
		    replicasize=20;

		    sql = " select
				rev_amt = sum(:i:a_amount),
				rev_type = :a:a_account_code + ' ' + :a:a_acct_desc
			    from
				identity /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows i,
				/apps/kardia/data/Kardia_DB/a_account/rows a
			    where
				:a:a_ledger_number = :i:a_ledger_number and
				:a:a_account_code = :i:a_account_code
			    group by
				:i:a_account_code
			    ";

		    rcpt_rev_link "widget/rule"
			{
			ruletype=osrc_relationship;
			target=rcpt_osrc;
			key_1=a_ledger_number;
			key_2=a_batch_number;
			target_key_1=a_ledger_number;
			target_key_2=a_batch_number;
			key_objname=i;
			revealed_only=yes;
			}

		    rev_table "widget/table"
			{
			height=90;
			titlebar=yes;
			colsep=0;
			overlap_scrollbar=yes;
			demand_scrollbar=yes;
			show_selection=no;
			rowheight=null;
			cellvspacing=4;
			inner_padding=2;
			row_border_radius=6;
			row_shadow_radius=2;
			row_shadow_offset=1;
			row_shadow_color="#a0a0a0";
			row_shadow_angle=135;

			rt_revtype "widget/table-column" { title="Revenue Category:"; width=200; fieldname=rev_type; style=bold; }
			rt_amount "widget/table-column" { title=""; width=200; fieldname=rev_amt; align=right; }
			}
		    }

		gt_osrc "widget/osrc"
		    {
		    readahead=20;
		    replicasize=20;

		    sql = " select
				gt_amt = sum(:i:a_amount),
				gt_type = :gt:a_payment_cost_center + '/' + :gt:a_payment_account_code + ' ' + :gt:a_gift_payment_type_desc
			    from
				identity /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows i,
				/apps/kardia/data/Kardia_DB/a_gift_payment_type/rows gt
			    where
				:gt:a_ledger_number = :i:a_ledger_number and
				:gt:a_gift_payment_type = :i:a_dn_gift_type
			    group by
				:i:a_dn_gift_type
			    ";

		    rcpt_gt_link "widget/rule"
			{
			ruletype=osrc_relationship;
			target=rcpt_osrc;
			key_1=a_ledger_number;
			key_2=a_batch_number;
			target_key_1=a_ledger_number;
			target_key_2=a_batch_number;
			key_objname=i;
			revealed_only=yes;
			}

		    gt_table "widget/table"
			{
			height=90;
			titlebar=yes;
			colsep=0;
			overlap_scrollbar=yes;
			demand_scrollbar=yes;
			show_selection=no;
			rowheight=null;
			cellvspacing=4;
			inner_padding=2;
			row_border_radius=6;
			row_shadow_radius=2;
			row_shadow_offset=1;
			row_shadow_color="#a0a0a0";
			row_shadow_angle=135;

			gt_type "widget/table-column" { title="Gift/Payment Type:"; width=200; fieldname=gt_type; style=bold; }
			gt_amount "widget/table-column" { title=""; width=200; fieldname=gt_amt; align=right; }
			}
		    }

		gl_post_ctls_hbox "widget/hbox"
		    {
		    height=24;
		    spacing=10;
		    align=center;

		    gl_post_ok "widget/textbutton"
			{
			text = "Post";

			doglpost_cn "widget/connector"
			    {
			    event=Click;
			    target=post_cr;
			    action=GLPost;
			    a_ledger_number=runclient(:cr_batches:ledger);
			    a_period=runclient(:periods_osrc:a_period);
			    a_batch_number=runclient(:rcpt_osrc:a_batch_number);
			    }
			win_close_cn "widget/connector" { event=Click; target=post_to_gl_window; action=Close; }
			}
		    gl_post_cancel "widget/textbutton"
			{
			text = "Cancel";
			cancel_cn "widget/connector" { event=Click; target=post_to_gl_window; action=Close; }
			}
		    }
		}
	    }

	new_batch_window "widget/childwindow"
	    {
	    style=dialog;
	    titlebar=no;
	    modal=yes;
	    toplevel=yes;
	    width=480; height=300;
	    x=150; y=140;
	    visible=no;
	    border_radius=8;

	    new_batch_form "widget/form"
		{
		new_batch_cmp "widget/component"
		    {
		    x=0;y=0;width=478;height=298;
		    path="/apps/kardia/modules/gl/generic_editbatch.cmp";
		    ledger=runserver(:this:ledger);
		    periods_osrc = periods_osrc;
		    title = "Cash Receipts";
		    origin = "CR";
		    new_batch_window = new_batch_window;
		    new_batch_form = new_batch_form;
		    allow_effdate = 1;
		    }
		}
	    }

	batch_tbl_pane "widget/pane"
	    {
	    x=0;y=0; width=600;height=542;
	    widget_class = "table_bgnd";

	    batch_tbl "widget/table"
		{
		x=0;y=0;width=598;height=540;
		mode=dynamicrow;

		t_bat "widget/table-column" { title="Batch"; fieldname="a_batch_number"; width=50; }
		t_desc "widget/table-column" { title="Description"; fieldname="a_batch_desc"; width=150; }
		t_amt "widget/table-column"
		    {
		    title="Amount";
		    fieldname="amt";
		    width=65;
		    align=right; 
		    require_one_endorsement="kardia:gift_manage","kardia:gift_entry","kardia:gift_amt";
		    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
		    }
		t_dt "widget/table-column" { title="Date"; fieldname="date_txt"; width=75; }
		t_user "widget/table-column" { title="User"; fieldname="s_created_by"; width=50; }
		t_rpost "widget/table-column" { title="Post?"; fieldname="rpost"; width=35; type=check; }
		t_print "widget/table-column" { title="Print?"; fieldname="print"; width=35; type=check; }
		t_glcnt "widget/table-column" { title="GL?"; fieldname="glrec"; width=35; type=check; }
		}
	    }
	}

    btn_vbox "widget/vbox"
	{
	x = 608; y=0; width=172; height=542;
	spacing=8;
	cellsize=40;

	new_rcpt "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = "New Batch";
	    enabled = runclient(:new_batch_form:is_newable and :periods_osrc:a_status == 'O');

	    new_cn "widget/connector" { event=Click; target=new_batch_form; action=New; }
	    }
	edit_details "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = "Edit Batch Details";
	    enabled = runclient(:new_batch_form:is_editable and :periods_osrc:a_status == 'O');

	    detail_cn "widget/connector" { event=Click; target=new_batch_form; action=Edit; }
	    }
	edit_rcpt2 "widget/textbutton"
	    {
	    invert_security_check=yes;
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = runclient(isnull("View Receipts for #" + :rcpt_form:a_batch_number, ""));
	    enabled = runclient(:rcpt_form:rpost == 1);

	    edit2_cn "widget/connector"
		{
		event=Click;
		target=cr_batches;
		action=Launch;
		Source = runclient("/apps/kardia/modules/rcpt/gift_entry_new.app");
		Width=1000;
		Height=700;
		ledger=runclient(:rcpt_osrc:a_ledger_number);
		batch=runclient(:rcpt_osrc:a_batch_number);
		}
	    }
	edit_rcpt "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = runclient(isnull(condition(:rcpt_form:rpost == 1, "View", "Enter") + " Receipts for #" + :rcpt_form:a_batch_number, ""));
	    enabled = runclient(:rcpt_form:a_batch_number is not null);
	    //enabled = runclient(:rcpt_form:is_editable and :rcpt_form:rpost != 1 and :rcpt_form:glrec != 1);

	    edit_cn "widget/connector"
		{
		event=Click;
		event_condition=runclient(:rcpt_osrc:s_created_by == user_name() or :rcpt_form:rpost == 1);
		target=cr_batches;
		action=Launch;
		Source = runclient("/apps/kardia/modules/rcpt/gift_entry_new.app");
		Width=1000;
		Height=700;
		ledger=runclient(:rcpt_osrc:a_ledger_number);
		batch=runclient(:rcpt_osrc:a_batch_number);
		}
	    edit_cn2 "widget/connector"
		{
		event=Click;
		event_condition=runclient(:rcpt_osrc:s_created_by != user_name() and not (:rcpt_form:rpost == 1));
		event_confirm=runclient("The batch " + :rcpt_osrc:a_batch_number + " was started by someone else (" + :rcpt_osrc:s_created_by + ").  Do you really want to enter receipts in this batch?");
		target=cr_batches;
		action=Launch;
		Source = runclient("/apps/kardia/modules/rcpt/gift_entry_new.app");
		Width=1000;
		Height=700;
		ledger=runclient(:rcpt_osrc:a_ledger_number);
		batch=runclient(:rcpt_osrc:a_batch_number);
		}
	    }
	sep_line2 "widget/pane" 
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    height=2; fl_height=0; style=lowered; 
	    }
	posting_lbl "widget/label"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    height=15; text="Posting"; align=center; style=bold; 
	    }
	post "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = runclient(condition(:rcpt_form:rpost > 0 and has_endorsement('kardia:gift_manage','kardia:ledger:' + :ledger:value), condition(:rcpt_form:glrec > 0, "Request Unpost from CR and GL", "Request Unpost from CR"), "Post Batch To CR Journal"));
	    enabled = runclient(:rcpt_form:rrec > 0 and :rcpt_form:is_editable and ((:rcpt_form:glrec != 1 and :rcpt_form:rpost != 1) or has_endorsement('kardia:gift_manage','kardia:ledger:' + :ledger:value)));

	    dopost_cn "widget/connector"
		{
		event=Click;
		event_condition = runclient(:rcpt_form:is_editable and :rcpt_form:rrec == 1 and :rcpt_form:glrec != 1 and :rcpt_form:rpost != 1);
		event_confirm = runclient("Really post batch #" + :rcpt_form:a_batch_number + " to the Cash Receipts journal?");
		target=post_cr;
		action=Post;
		a_ledger_number=runclient(:cr_batches:ledger);
		a_period=runclient(:periods_osrc:a_period);
		a_batch_number=runclient(:rcpt_osrc:a_batch_number);
		}

	    unpostcr_cn "widget/connector"
		{
		event=Click;
		event_condition = runclient(not (:rcpt_form:is_editable and :rcpt_form:rrec == 1 and :rcpt_form:glrec != 1 and :rcpt_form:rpost != 1));
		target=unpostcr_win;
		action=Open;
		IsModal=1;
		gl_only=0;
		}
	    }
	post_to_gl "widget/textbutton"
	    {
	    require_endorsements="kardia:gift_manage";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    //condition = runserver((select count(1) from /apps/kardia/data/Kardia_DB/s_sec_endorsement/rows where :s_endorsement = 'gift_manage' and :s_context = 'ledger=' + :this:ledger and :s_subject = 'u:' + user_name()) > 0 or (select count(1) from /apps/kardia/data/Kardia_DB/s_sec_endorsement/rows where :s_endorsement = 'gift_manage' and :s_context = 'ledger=' + :this:ledger) == 0);
	    width = 172;
	    height = 40;
	    text = runclient(condition(:rcpt_form:glrec > 0 and has_endorsement('kardia:gift_manage','kardia:ledger:' + :ledger:value), "Request Unpost from GL", "Post Batch To GL"));
	    enabled = runclient(:rcpt_form:rrec > 0 and :rcpt_form:is_editable and ((:rcpt_form:glrec != 1 and :rcpt_form:rpost == 1) or has_endorsement('kardia:gift_manage','kardia:ledger:' + :ledger:value)));

	    win_open_cn "widget/connector"
		{
		event=Click;
		event_condition=runclient(:rcpt_form:rrec > 0 and :rcpt_form:is_editable and :rcpt_form:glrec == 0 and :rcpt_form:rpost > 0);
		//event_condition=runclient(:rcpt_form:is_editable and :rcpt_form:glrec != 1 and :rcpt_form:rpost == 1);
		target=post_to_gl_window;
		action=Open;
		IsModal=1;
		}
	    unpostgl_cn "widget/connector"
		{
		event=Click;
		event_condition = runclient(:rcpt_form:rrec > 0 and :rcpt_form:is_editable and :rcpt_form:glrec >= 1 and :rcpt_form:rpost > 0);
		//event_condition = runclient(not (:rcpt_form:is_editable and :rcpt_form:rrec == 1 and :rcpt_form:glrec != 1 and :rcpt_form:rpost != 1));
		target=unpostcr_win;
		action=Open;
		IsModal=1;
		gl_only=1;
		}
	    }
	sep_line3 "widget/pane" 
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    height=2; fl_height=0; style=lowered; 
	    }
	reporting_lbl "widget/label"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    height=15; text="Reports and Printing"; align=center; style=bold; 
	    }
	print_cks "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = "Report: Receipted Checks";
	    enabled = runclient(:rcpt_form:is_editable and :rcpt_form:rrec == 1);

	    doprintcks_cn "widget/connector"
		{
		event=Click;
		target = cr_batches;
		action= Launch;
		Source = runclient("/apps/kardia/modules/rcpt/donation_checks.rpt");
		Width = 800;
		Height = 600;
		ledger = runclient(:periods_osrc:a_ledger_number);
		batch = runclient(:rcpt_osrc:a_batch_number);
		}
	    }
	print_detail "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = "Report: Donation Detail";
	    enabled = runclient(:rcpt_form:is_editable and :rcpt_form:rrec == 1);

	    doprintdetail_cn "widget/connector" { event=Click; target = cr_batches; action = Launch; Source=runclient("/apps/kardia/modules/rcpt/donation_detail.rpt"); Width=800; Height=600; ledger=runclient(:periods_osrc:a_ledger_number); start_period=runclient(:periods_osrc:a_period); end_period=runclient(:periods_osrc:a_period); batch=runclient(:rcpt_osrc:a_batch_number); unposted=1; show_line_items=1; }
	    }
	print_rcpts "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = "Print Receipts";
	    enabled = runclient(:rcpt_form:is_editable and :rcpt_form:rpost == 1);

	    doprintrcpts_cn "widget/connector" { event=Click; target = print_rcpt_window; action = Open; }
	    }
	rcprcpt "widget/textbutton"
	    {
	    require_one_endorsement="kardia:gift_manage","kardia:gift_entry";
	    endorsement_context=runserver("kardia:ledger:" + :this:ledger + ":");
	    width = 172;
	    height = 40;
	    text = runclient("Report: Cash Receipts " + isnull(:periods_osrc:a_period, ''));
	    enabled = runclient(:rcpt_form:is_editable);

	    rpt_print_cn "widget/connector" { event="Click"; target="cr_batches"; action="Launch"; Source=runclient("/apps/kardia/modules/rcpt/donation_summary.rpt"); Width=runclient(800); Height=runclient(600); ledger=runclient(:periods_osrc:a_ledger_number); period=runclient(:periods_osrc:a_period); unposted=1; }
	    }
	}
    }
