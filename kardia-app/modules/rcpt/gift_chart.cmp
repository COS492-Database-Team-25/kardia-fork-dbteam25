$Version=2$
gift_chart "widget/component-decl"
    {
    width=654;
    height=135;

    ledger_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }
    bar_color "widget/parameter" { type=string; default="#3e7d38"; }
    chart_title "widget/parameter" { type=string; }
    months "widget/parameter" { type=integer; default=25; deploy_to_client=yes; }
    fund "widget/parameter" { type=string; deploy_to_client=yes; }
    partner "widget/parameter" { type=string; deploy_to_client=yes; }

    chart_osrc "widget/osrc"
	{
	chart_ledger "widget/parameter" { param_name=ledger; type=string; }
	chart_months "widget/parameter" { param_name=months; type=integer; default=runclient(:months:value); }
	chart_fund "widget/parameter" { param_name=fund; type=string; default=runclient(:fund:value); }
	chart_partner "widget/parameter" { param_name=partner; type=string; default=runclient(:partner:value); }
	//chart_fund "widget/parameter" { param_name=fund; type=string; }
	//chart_period "widget/parameter" { param_name=period; type=string; }

	sql = "	declare object info;

		select
		    :info:cclist = isnull( (select sum(:cc:a_ledger_number + '|' + :cc:a_cost_center + ',') - ',' from /apps/kardia/data/Kardia_DB/a_cc_staff/rows cs, /apps/kardia/data/Kardia_DB/a_cost_center/rows cc, /apps/kardia/data/Kardia_DB/p_staff/rows s where :cs:p_staff_partner_key = :s:p_partner_key and (:cc:a_bal_cost_center = :cs:a_cost_center or :cc:a_cost_center = :cs:a_cost_center) and :cc:a_ledger_number = :cs:a_ledger_number and :s:p_kardia_login = user_name()), '')
		;

		select
		    :info:endperiod = :a_period
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows
		where
		    :a_ledger_number = :parameters:ledger and
		    :a_summary_only = 0 and
		    :a_start_date <= getdate()
		order by
		    :a_end_date desc
		limit
		    1
		;

		select
		    :info:startperiod = :a_period
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows
		where
		    :a_ledger_number = :parameters:ledger and
		    :a_summary_only = 0 and
		    :a_start_date > dateadd(month, 0 - :parameters:months, getdate())
		order by
		    :a_end_date asc
		limit
		    1
		;

		select
		    month = dateformat(:p:a_start_date, 'MMM\\'yy'),
		    year = datepart(year, :p:a_start_date),
		    Amount = convert(integer,sum(:g:a_amount)),
		    total = sum(:g:a_amount) --,
		    --idx = datediff(month, :start_period_qy:a_start_date, :p:a_start_date)
		from
		    /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g,
		    /apps/kardia/data/Kardia_DB/a_period/rows p
		where
		    :p:a_ledger_number = :parameters:ledger and
		    :p:a_period >= :info:startperiod and
		    :p:a_period <= :info:endperiod and
		    :p:a_summary_only = 0 and
		    :g:a_posted = 1 and
		    :p:a_period *= :g:a_period and
		    :p:a_ledger_number *= :g:a_ledger_number and
		    (:parameters:fund is null or :parameters:fund = :g:a_cost_center) and
		    (:parameters:partner is null or :parameters:partner = :g:p_dn_donor_partner_id)
		group by
		    :p:a_period
		order by
		    :p:a_period asc
		having
		    has_endorsement('kardia:gift_amt', 'kardia:ledger:' + :parameters:ledger)
		;

		select
		    month = dateformat(:p:a_start_date, 'MMM\\'yy'),
		    year = datepart(year, :p:a_start_date),
		    Amount = convert(integer,sum(:g:a_amount)),
		    total = sum(:g:a_amount) --,
		    --idx = datediff(month, :start_period_qy:a_start_date, :p:a_start_date)
		from
		    /apps/kardia/data/Kardia_DB/a_subtrx_gift_item/rows g,
		    /apps/kardia/data/Kardia_DB/a_period/rows p
		where
		    :p:a_ledger_number = :parameters:ledger and
		    :p:a_period >= :info:startperiod and
		    :p:a_period <= :info:endperiod and
		    :g:a_posted = 1 and
		    :p:a_summary_only = 0 and
		    :p:a_period *= :g:a_period and
		    :p:a_ledger_number *= :g:a_ledger_number and
		    (:parameters:fund is null or :parameters:fund = :g:a_cost_center) and
		    charindex(',' + :parameters:ledger + '|' + :g:a_cost_center + ',', ',' + :info:cclist + ',') > 0 and
		    (:parameters:partner is null or :parameters:partner = :g:p_dn_donor_partner_id)
		group by
		    :p:a_period
		order by
		    :p:a_period asc
		having
		    not has_endorsement('kardia:gift_amt', 'kardia:ledger:' + :parameters:ledger)
		";
	readahead=30;
	replicasize=30;
	//autoquery=onload;
	autoquery=oneachreveal;

	chart_link "widget/rule"
	    {
	    ruletype=osrc_relationship;
	    target=ledger_osrc;
	    target_key_1=Ledger;
	    key_1=ledger;
	    target_key_2=Fund;
	    key_2=fund;
	    revealed_only=yes;
	    }

	chart "widget/chart"
	    {
	    x=0;
	    y=0;
	    width=654;
	    height=135;
	    fl_width=10;
	    fl_height=10;
	    chart_type="bar";
	    title=runserver(:this:chart_title);
	    titlecolor="#334466";
	    //legend_position="right";
	    start_at_zero=yes;
	    title_size=16;

	    series "widget/chart-series"
		{
		color=runserver(:this:bar_color);
		y_column="Amount";
		}
	    
	    y_axis "widget/chart-axis" 
		{
		axis="y";
		label="Giving";
		}

	    x_axis "widget/chart-axis" 
		{
		condition=0;
		label="Month";
		}
	    }
	}
    }
