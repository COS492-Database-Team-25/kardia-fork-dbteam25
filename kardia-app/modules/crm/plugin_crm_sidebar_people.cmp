$Version=2$
people_list "widget/component-decl"
    {
    height=417; width=248;

    title = "People";
    icon = "/apps/kardia/images/icons/ionicons-people.svg";
    sequence = 10;

    PartnerSelected "widget/component-decl-event" { }

    people_vbox "widget/vbox"
	{
	x=0; y=0; width=248; height=413;
	spacing=8;

	active_collab_osrc "widget/osrc"
	    {
	    sql = " select
			collaborator_id = :c:e_collaborator,
			collaborator_name = (select condition(char_length(isnull(:p_org_name,'')) > 0, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:e_collaborator),
			collaborator_type_id = :c:e_collab_type_id, 
			collaborator_type = (select :e_collab_type_label from /apps/kardia/data/Kardia_DB/e_collaborator_type/rows where :e_collab_type_id = :c:e_collab_type_id),
			partner_id = :c:p_partner_key,
			partner_name = (select condition(char_length(isnull(:p_org_name,'')) > 0, :p_org_name, :p_given_name + ' ' + :p_surname) from /apps/kardia/data/Kardia_DB/p_partner/rows where :p_partner_key = :c:p_partner_key),
			img = isnull( ( select
				    path = :d:e_current_folder + '/' + :d:e_current_filename
				from
				    /apps/kardia/data/Kardia_DB/e_document/rows d,
				    /apps/kardia/data/Kardia_DB/e_partner_document/rows pd,
				    /apps/kardia/data/Kardia_DB/e_document_type/rows dt
				where
				    :dt:e_doc_type_label = 'Profile Photo' and
				    :d:e_doc_type_id = :dt:e_doc_type_id and
				    :pd:e_document_id = :d:e_document_id and
				    :pd:p_partner_key = :c:e_collaborator
				order by
				    :pd:s_date_modified desc
				limit
				    1
				), '/apps/kardia/images/artwork/persona.png'),
			last_date = substring(convert(string,getdate()),1,11)
		    from
			identity /apps/kardia/data/Kardia_DB/e_collaborator/rows c,
			/apps/kardia/data/Kardia_DB/p_staff/rows s
		    where
			:c:e_collaborator = :s:p_partner_key and
			:s:p_kardia_login = user_name()
		    ";
	    readahead=50;
	    replicasize=50;
	    
	    active_collab_table "widget/table"
		{
		height=381;
		//rowheight = 20;
		min_rowheight=16;
		max_rowheight=64;
		mode=dynamicrow;
		allow_selection = yes;
		show_selection = yes;
		initial_selection = no;
		demand_scrollbar = yes;
		overlap_scrollbar = yes;
		colsep = 0;
		titlebar = no;
		//row_border_color="#e0e0e0";
		row_border_radius=4;
		//row_shadow_offset=1;
		//row_shadow_color="#c0c0c0";
		inner_padding = 2;
		rowhighlight_bgcolor = "#fff090";
		textcolorhighlight = "#000000";
		cellvspacing = 4;
		row1_bgcolor = "#f8f8f8";
		row2_bgcolor = "#f8f8f8";

		onclick "widget/connector" { event=Click; target=people_list; action=PartnerSelected; partner_key=runclient(:partner_id); }

		t_img "widget/table-column" { title=""; fieldname="img"; width=40; type=image; image_maxwidth=32; image_maxheight=32; align=center; }
		t_name "widget/table-column" { title="Partner"; fieldname="partner_name"; width=208; caption_fieldname=last_date; caption_textcolor="#9b8707"; style=bold; font_size=18; caption_style=italic; }
		}
	    }

	role_select_dd "widget/dropdown"
	    {
	    x=4;
	    height=24;
	    width=240;
	    mode=dynamic_server;
	    sql = " select
			'All of My People',
			'*',
			1
		    ;

		    select
			:e_collab_type_label,
			convert(string,:e_collab_type_id),
			0
		    from
			/apps/kardia/data/Kardia_DB/e_collaborator_type/rows
		    ";

	    role_dd_hints "widget/hints" { style=notnull; }
	    }
	}
    }
