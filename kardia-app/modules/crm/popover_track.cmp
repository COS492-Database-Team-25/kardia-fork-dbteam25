$Version=2$
popover_track "widget/component-decl"
    {
    width=1000; height=600;

    Open "widget/component-decl-action" { }

    TrackUpdated "widget/component-decl-event" { }

    onOpen1 "widget/connector" { event=Open; target=partner_var; action=SetValue; Value=runclient(:partner); }
    onOpen2 "widget/connector" { event=Open; target=collab_var; action=SetValue; Value=runclient(:collaborator); }
    onOpen3 "widget/connector" { event=Open; target=partnername_var; action=SetValue; Value=runclient(:partnername); }
    onOpen4 "widget/connector" { event=Open; target=engagement_var; action=SetValue; Value=runclient(:engagement_id); }
    onOpen5 "widget/connector" { event=Open; target=track_win; action=Open; IsModal=1; PointAt=runclient(:PointAt); PointSide=runclient(:PointSide); PointOffset=runclient(:PointOffset); }

    partner_var "widget/variable" { type=string; }
    partnername_var "widget/variable" { type=string; }
    collab_var "widget/variable" { type=string; }
    engagement_var "widget/variable" { type=integer; }

    track_win "widget/childwindow"
	{
	width=600; height=344;
	x=250; y=89;
	fl_y=0;
	titlebar=no;
	visible=no;
	border_radius=8;
	border_style=solid;
	border_color="#6080c0";
	shadow_radius=8;
	shadow_color="#6080c0";
	shadow_offset=0;
	background=null;
	bgcolor="#f8f8f8";

	track_vbox "widget/vbox"
	    {
	    x=15; y=15;
	    width=568; height=312;
	    spacing=10;

	    track_osrc "widget/osrc"
		{
		engagement_id "widget/parameter" { type=integer; default=runclient(:engagement_var:value); }
		partner "widget/parameter" { type=string; default=runclient(:partner_var:value); }
		sql = "	select
			    :pe:p_partner_key,
			    :pe:e_engagement_id,
			    :pe:e_hist_id,
			    :pe:e_track_id,
			    :pe:e_step_id,
			    :pe:e_is_archived,
			    :pe:e_completion_status,
			    :pe:e_desc,
			    :pe:e_comments,
			    :pe:e_start_date,
			    :pe:e_started_by,
			    :pe:e_completion_date,
			    :pe:e_completed_by,
			    :pe:e_exited_date,
			    :pe:e_exited_by,
			    :pe:s_date_created,
			    :pe:s_created_by,
			    :pe:s_date_modified,
			    :pe:s_modified_by,
			    :t:e_track_name
			from
			    identity /apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe,
			    /apps/kardia/data/Kardia_DB/e_engagement_track/rows t
			where
			    :pe:e_engagement_id = :parameters:engagement_id and
			    :pe:p_partner_key = :parameters:partner and
			    :pe:e_is_archived = 0 and
			    :t:e_track_id = :pe:e_track_id
			order by
			    charindex(:pe:e_completion_status, 'ICE') asc,
			    :pe:e_start_date desc
			limit
			    1
			";
		baseobj = "/apps/kardia/data/Kardia_DB/e_partner_engagement/rows";
		replicasize=2;
		readahead=2;
		autoquery=oneachreveal;

		sync_on_endquery "widget/connector" { event=EndQuery; target=steplist_osrc; action=QueryParam; }

		track_form "widget/form"
		    {
		    allow_search=no;
		    allow_view=yes;
		    allow_modify=yes;
		    allow_new=yes;

		    close_on_save "widget/connector" { event=DataSaved; target=track_win; action=Close; }
		    refresh_profile_on_save "widget/connector" { event=DataSaved; target=popover_track; action=TrackUpdated; }
		    close_on_discard "widget/connector" { event=Discard; target=track_win; action=Close; }

		    hdr_label "widget/label" { height=20; font_size=16; style=bold; value=runclient(:track_osrc:e_track_name + ' - ' + :track_osrc:e_desc + "..."); align=center; }

		    steps_label "widget/label" { height=14; style=bold; value=runclient('Engagement Steps:'); }

		    steplist_pane "widget/pane"
			{
			style=bordered;
			border_radius=8;
			border_color="#6080c0";
			height=123;

			steplist_osrc "widget/osrc"
			    {
			    step_engagement_id "widget/parameter" { type=integer; default=runclient(:engagement_var:value); }
			    step_partner "widget/parameter" { type=string; default=runclient(:partner_var:value); }
			    step_track "widget/parameter" { type=integer; default=runclient(:track_osrc:e_track_id); }
			    sql = " select
					:s:e_step_name,
					:s:e_step_description,
					:pe:e_desc,
					:pe:e_comments,
					:pe:e_completion_status,
					:pe:e_started_by,
					start_name = condition(char_length(isnull(:p:p_org_name,'')) > 1, :p:p_org_name, condition(char_length(:p:p_preferred_name) > 1, :p:p_preferred_name, :p:p_given_name) + ' ' + :p:p_surname),
					end_name = condition(char_length(isnull(:p2:p_org_name,'')) > 1, :p2:p_org_name, condition(char_length(:p2:p_preferred_name) > 1, :p2:p_preferred_name, :p2:p_given_name) + ' ' + :p2:p_surname),
					start_date = substring(convert(string,:pe:e_start_date),1,11),
					end_date = substring(convert(string,isnull(:pe:e_completion_date,:pe:e_exited_date)),1,11)
				    from
					/apps/kardia/data/Kardia_DB/e_engagement_step/rows s,
					/apps/kardia/data/Kardia_DB/e_partner_engagement/rows pe,
					/apps/kardia/data/Kardia_DB/p_partner/rows p,
					/apps/kardia/data/Kardia_DB/p_partner/rows p2
				    where
					:s:e_track_id = :parameters:step_track and
					:pe:e_track_id =* :s:e_track_id and
					:pe:e_step_id =* :s:e_step_id and
					:pe:e_engagement_id = :parameters:step_engagement_id and
					:pe:p_partner_key = :parameters:step_partner and
					:p:p_partner_key =* :pe:e_started_by and
					:p2:p_partner_key =* isnull(:pe:e_completed_by, :pe:e_exited_by)
				    order by
					:s:e_step_sequence
				    ";
			    replicasize=20;
			    readahead=20;
			    autoquery=never;

			    steplist_table "widget/table"
				{
				x=8; y=8; width=550; height=105;
				min_rowheight=21;
				max_rowheight=64;
				allow_selection = yes;
				show_selection = yes;
				initial_selection = no;
				demand_scrollbar = yes;
				overlap_scrollbar = yes;
				colsep = 0;
				titlebar = no;
				row_border_radius=4;
				inner_padding = 0;
				cellvspacing = 0;
				row1_bgcolor = "#f8f8f8";
				row2_bgcolor = "#f8f8f8";
				row_border_radius=4;
				rowhighlight_bgcolor = "#fff090";
				textcolorhighlight = "#000000";

				t_checkmark "widget/table-column" { value=runclient(condition(:steplist_osrc:e_completion_status is null, '/sys/images/trans_1.gif', condition(:steplist_osrc:e_completion_status = 'I', '/sys/images/dotted_check.gif', condition(:steplist_osrc:e_completion_status == 'C', '/sys/images/green_check.gif', '/sys/images/red_x.gif')))); width=24; type=image; image_maxwidth=16; image_maxheight=16; align=left; }
				t_stepname "widget/table-column" { fieldname="e_step_name"; width=100; style=bold; }
				t_datewho "widget/table-column" { value=runclient( isnull(condition(:steplist_osrc:e_completion_status = 'C', 'Complete: ', 'Exited: ') + :steplist_osrc:start_date + ' - ' + :steplist_osrc:end_date + '\n', isnull('Started: ' + :steplist_osrc:start_date + '\n',''))); width=422; wrap=yes; caption_style=italic; caption_textcolor="#606060"; caption_value=runclient(:steplist_osrc:e_comments); }
				//t_comm "widget/table-column" { fieldname=e_comments; wrap=yes; width=182; style=italic; }
				}
			    }
			}

		    eng_desc "widget/component" { field=e_desc; path="/sys/cmp/smart_field.cmp"; width=300; height=20; text="Note:"; ctl_type=editbox; label_width=70; empty_desc="40 chars max"; }
		    eng_comments "widget/component" { field=e_comments; path="/sys/cmp/smart_field.cmp"; height=60; text="Comments:"; ctl_type=textarea; label_width=70; }

		    track_partner "widget/variable" { fieldname=p_partner_key; track_partner_hints "widget/hints" { default=runclient(:partner_var:value); } }
		    track_me "widget/variable" { fieldname=e_started_by; track_me_hints "widget/hints" { default=runclient(:collab_var:value); } }
		    track_startdate "widget/variable" { fieldname=e_start_date; track_start_hints "widget/hints" { default=runclient(getdate()); } }
		    track_archived "widget/variable" { fieldname=e_is_archived; track_arc_hints "widget/hints" { default=runclient(0); } }
		    track_status "widget/variable" { fieldname=e_completion_status; track_comp_hints "widget/hints" { default=runclient('I'); } }
		    track_meta "widget/component" { path="/apps/kardia/modules/base/record_metadata_hidden.cmp"; }

		    track_btns "widget/hbox"
			{
			height=24;
			align=center;
			spacing=10;

			add_btn "widget/textbutton"
			    {
			    height=24; width=150;
			    text = "Update Track";
			    background="/apps/kardia/images/bg/lsblue_btngradient.png";
			    fgcolor1=white;
			    fgcolor2=black;
			    disable_color="#334466";

			    enabled = runclient(0);
			    //enabled = runclient(:track_form:is_savable and :track_type:value is not null and :track_step_id:value is not null and char_length(:track_desc:content) > 0);

			    on_add "widget/connector" { event=Click; target=track_form; action=Save; }
			    }

			cancel_btn "widget/textbutton"
			    {
			    height=24; width=150;
			    text = "Cancel";
			    background="/apps/kardia/images/bg/lsblue_btngradient.png";
			    fgcolor1=white;
			    fgcolor2=black;
			    disable_color="#334466";

			    on_cancel1 "widget/connector" { event=Click; target=track_form; action=Discard; }
			    on_cancel2 "widget/connector" { event=Click; target=track_win; action=Close; }
			    }
			}
		    }
		}
	    }
	}
    }
