$Version=2$
profile_timeline "widget/component-decl"
    {
    width=442;
    height=450;

    partner_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }
    
    profile_timeline_vbox "widget/vbox"
	{
	x=0; y=0;
	width=442; height=450;
	spacing=8;

	profile_timeline_menu "widget/menu"
	    {
	    widget_class="bar";
	    height=24;

	    profile_timeline_menu_add "widget/menu"
		{
		widget_class="popup";
		label = "Add";

		menu_add_contact "widget/menuitem" { label = "Contact"; }
		menu_add_note "widget/menuitem" { label = "Prayer Need / Note"; }
		menu_add_interest "widget/menuitem" { label = "Tag / Area of Interest"; }
		menu_add_track "widget/menuitem" { label = "Engagement Track"; }
		menu_add_req "widget/menuitem" { label = "Requirement"; }
		menu_add_doc "widget/menuitem" { label = "Document / File / Photo"; }
		menu_add_collab "widget/menuitem" { label = "Collaborator"; }
		menu_add_todo "widget/menuitem" { label = "To-Do Item"; }
		}

	    profile_timeline_menu_view "widget/menu"
		{
		widget_class="popup";
		label = "View";

		menu_view_all "widget/menuitem" { label = "All"; checked=yes; }
		menu_view_note "widget/menuitem" { label = "Only Prayer Needs / Notes"; checked=no; }
		menu_view_track "widget/menuitem" { label = "Only Track and Step Progress"; checked=no; }
		}

	    profile_timeline_menu_sort "widget/menu"
		{
		widget_class="popup";
		label = "Sort";

		menu_sort_newest "widget/menuitem" { label = "By Most Recent"; checked=yes; }
		menu_sort_type "widget/menuitem" { label = "By Kind"; checked=no; }
		}

	    profile_timeline_menu_reset "widget/menuitem"
		{
		label = "Reset";
		onright = yes;

		reload_on_reset "widget/connector" { event=Select; target=timeline_osrc; action=Refresh; }
		}
	    }

	timeline_scroll_pane "widget/pane"
	    {
	    height=418;
	    style=flat;
	    //style=bordered;
	    //border_color="#e0e0e0";
	    //border_radius=2;
	    //bgcolor=white;

	    timeline_osrc "widget/osrc"
		{
		sql = " select
			    img = condition(:e:e_activity_type == 'ADDR', '/apps/kardia/images/tango/go-home.png',
				    condition(:e:e_activity_type == 'CONT', '/apps/kardia/images/tango/contact-new.png',
				      condition(:e:e_activity_type == 'FILE', '/apps/kardia/images/tango/document-new.png',
				        condition(:e:e_activity_type == 'NOTE', '/apps/kardia/images/tango/format-justify-left.png',
				          condition(:e:e_activity_type == 'PAR', '/apps/kardia/images/artwork/persona-25x26.png', 
					    condition(:e:e_activity_type == 'TRK', '/apps/kardia/images/tango/start-here.png', 
					  '')))))),
			    :e:e_activity_date,
			    :e:e_activity_type,
			    :e:e_reference_info,
			    :e:e_info,
			    shortdate = substring(convert(string,:e:e_activity_date),1,11),
			    datewhom = condition(char_length(isnull(:p:p_org_name,'')) > 0, :p:p_org_name, :p:p_given_name + ' ' + :p:p_surname) + ' on ' + substring(convert(string,:e:e_activity_date),1,11)
			from
			    '/apps/kardia/modules/crm/activity.qy?partner_key=000000' e,
			    /apps/kardia/data/Kardia_DB/p_partner/rows p
			where
			    :p:p_partner_key =* :e:e_whom
			";
		baseobj = "/apps/kardia/modules/crm/activity.qy?partner_key=000000";

		refresh_on_partner_change "widget/connector" { source=partner_osrc; event=EndQuery; action=ChangeSource; Source=runclient("/apps/kardia/modules/crm/activity.qy?partner_key=" + :partner_osrc:p_partner_key); }

		timeline_table "widget/table"
		    {
		    height=418;
		    width=442;
		    row_border_color="#e0e0e0";
		    row_border_radius=2;
		    row_shadow_offset=1;
		    row_shadow_color="#c0c0c0";
		    demand_scrollbar = yes;
		    overlap_scrollbar = yes;
		    colsep = 0;
		    titlebar = no;
		    min_rowheight = 16;
		    max_rowheight = 200;
		    cellvspacing = 4;
		    row1_bgcolor = white;
		    row2_bgcolor = white;
		    show_selection = yes;
		    inner_padding = 2;
		    rowhighlight_bgcolor = "#ffffc0";
		    textcolorhighlight = "#000000";

		    t_img "widget/table-column" { title=""; fieldname="img"; width=30; type=image; align=center; }
		    //t_date "widget/table-column" { title="Date"; fieldname="shortdate"; width=50; }
		    t_info "widget/table-column" { title="Description"; fieldname="e_info"; width=394; wrap=yes; caption_fieldname=datewhom; caption_textcolor = "#a0a0a0"; style=bold; caption_style=italic; }
		    }
		}

	    timeline_scroll "widget/scrollpane"
		{
		condition=0;
		x=0; y=0;
		width=442; height=418;

		timeline_data "widget/component"
		    {
		    width = 418;
		    height = 418;
		    mode = dynamic;
		    path = "timeline.cmp";

		    load_on_partner_change "widget/connector" { source=partner_osrc; event=EndQuery; action=Instantiate; partner_key=runclient(:partner_osrc:p_partner_key); }
		    }
		}
	    }
	}
    }
