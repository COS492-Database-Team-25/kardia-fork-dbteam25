$Version=2$
imbalance "system/report"
    {
    title = "GL Imbalance Research";

    document_format="application/pdf";
    resolution = 300;
    pagewidth=85;
    pageheight=66;
    marginleft=2.5;
    marginright=2.5;
    margintop=3;
    marginbottom=3;

    // Parameters for GL Fund Balance
    ledger "report/parameter" { type=string; default=null; }
    year_period "report/parameter" { type=string; default=runserver( (select :a_period from /apps/kardia/data/Kardia_DB/a_period/rows where datepart(month, :a_start_date) = 1 and datepart(month, :a_end_date) = 12 and datepart(year, :a_end_date) = datepart(year, getdate())) ); }
    ref_period "report/parameter" { type=string; default=runserver( (select first(:a_period) from /apps/kardia/data/Kardia_DB/a_period/rows where :a_summary_only = 0 and datepart(year, :a_end_date) = datepart(year, getdate()) order by :a_start_date) ); }
    end_period "report/parameter" { type=string; default=runserver( (select last(:a_period) from /apps/kardia/data/Kardia_DB/a_period/rows where :a_summary_only = 0 and datepart(year, :a_end_date) = datepart(year, getdate()) order by :a_start_date) ); }
    unposted "report/parameter" { type=integer; default=0; }

    ledger_qy "report/query"
	{
	sql = "select * from /apps/kardia/data/Kardia_DB/a_ledger/rows l where :l:a_ledger_number = :this:ledger";
	}

    accts_qy "report/query"
	{
	sql = runserver("
		select
			cc_code = max(condition(:c:a_cost_center == :c:a_bal_cost_center, :c:a_cost_center, '')),
			cc_desc = max(condition(:c:a_cost_center == :c:a_bal_cost_center, :c:a_cc_desc, '')),
			:c:a_bal_cost_center,
			:b:a_batch_desc,
			:tt:a_batch_number,
			cbal = isnull(sum(:tt:a_amount), $0.00),
			bbal = isnull(sum(condition(:tt:a_transaction_type = 'B', :tt:a_amount, $0.00)), $0.00) +
			       isnull(sum(condition(:tt:a_period < :this:ref_period, :tt:a_amount, $0.00)), $0.00)
		from
			/apps/kardia/data/Kardia_DB/a_cost_center/rows c,
			/apps/kardia/data/Kardia_DB/a_transaction/rows tt,
			/apps/kardia/data/Kardia_DB/a_batch/rows b
		where
			:tt:a_ledger_number = :this:ledger and
			:tt:a_cost_center = :c:a_cost_center and
			((:tt:a_period <= :this:end_period and
			 :tt:a_period >= :this:ref_period) or
			 :tt:a_period = :this:year_period) and
			:c:a_ledger_number = :this:ledger and
			:tt:a_ledger_number = :b:a_ledger_number and
			:tt:a_batch_number = :b:a_batch_number
			" + condition(:this:unposted, "", "and :tt:a_posted = 1") + "
		group by
			:c:a_bal_cost_center,
			:tt:a_batch_number
		having
			:cbal != $0.00
		");

	sum_b "report/aggregate" { compute="sum(:bbal)"; reset=0; }
	sum_c "report/aggregate" { compute="sum(:cbal)"; reset=0; }
	}

    ledger_form "report/form"
	{
	source = ledger_qy;

	rpt_title "report/area"
	    {
	    x=0; width=80; height=3;
	    font=helvetica;
	    align=center;
	    t_d1 "report/data" { value = runserver("General Imbalance Research for " + :ledger_qy:a_ledger_desc + " (" + :ledger_qy:a_ledger_number + ")\n"); }
	    t_d2 "report/data" { value = runserver("For Year " + :this:year_period + ", for periods " + :this:ref_period + " through " + :this:end_period + "\n"); }
	    t_d3 "report/data" { value = runserver("Requested by " + user_name() + " on " + getdate() + "\n"); }
	    t_d4 "report/data" { condition=runserver(:this:unposted); value = runserver("*** WARNING:  DATA INCLUDES UNPOSTED TRANSACTIONS ***\n"); }
	    }

	table "report/table"
	    {
	    x=0; width=80; height=0;
	    columns=5;
	    widths=8,25,9,25,9;
	    colsep=0.8;
	    //source=accts_qy,openbal_qy;
	    source=accts_qy;
	    topborder=0.3;
	    bottomborder=0.3;
	    nullformat="-";
	    moneyformat = "###,###,###.00-";

	    tbl_hdr "report/table-row"
		{
		font=helvetica;
		fontsize=8;
		header=yes;
		bottomborder=0.05;
		h_cc "report/table-cell" { value='Cost Ctr'; align=center; style=bold; }
		h_cdesc "report/table-cell" { value='Description'; align=center; style=bold; }
		h_batch "report/table-cell" { value='Batch'; align=center; style=bold; }
		h_bdesc "report/table-cell" { value='Description'; align=center; style=bold; }
		h_cur "report/table-cell" { value='Balance'; align=center; style=bold; }
		}

	    tbl_row "report/table-row"
		{
		font=courier;
		fontsize=8;
		marginbottom=0.0;
		d_cc "report/table-cell" { value=runserver(:accts_qy:cc_code); align=left; }
		d_cdesc "report/table-cell" { font=helvetica; value=runserver(:accts_qy:cc_desc); align=left; }
		d_batch "report/table-cell" { value=runserver(:accts_qy:a_batch_number); align=left; }
		d_bdesc "report/table-cell" { font=helvetica; value=runserver(:accts_qy:a_batch_desc); align=left; }
		d_cur "report/table-cell" { value=runserver(condition(:accts_qy:cbal == $0, '-0-   ', :accts_qy:cbal)); align=right; }
		}

	    tbl_sum_all "report/table-row"
		{
		summary=yes;
		font=courier;
		fontsize=8;
		topborder=0.3;
		//bottomborder=0.05;
		marginbottom=1.0;
		s3_cc "report/table-cell" { value=''; align=left; style=bold; }
		s3_cdesc "report/table-cell" { font=helvetica; value=runserver(""); align=right; style=bold; }
		s3_batch "report/table-cell" { value=''; align=left; style=bold; }
		s3_bdesc "report/table-cell" { font=helvetica; value=runserver("TOTAL:"); align=right; style=bold; }
		s3_cur "report/table-cell" { value=runserver(condition(:accts_qy:sum_c == $0, '-0-   ', :accts_qy:sum_c)); align=right; style=bold; }
		}
	    }
	}
    }
