$Version=2$
nca_chart "widget/component-decl"
    {
    width=257;
    height=135;

    ledger_osrc "widget/parameter" { type=object; find_container="widget/osrc"; }
    bar_color "widget/parameter" { type=string; default="#997C44"; }
    line_color "widget/parameter" { type=string; default="#DDB261"; }
    chart_title "widget/parameter" { type=string; }
    months "widget/parameter" { type=integer; default=13; deploy_to_client=yes; }
    fund "widget/parameter" { type=string; deploy_to_client=yes; }

    chart_osrc "widget/osrc"
	{
	chart_ledger "widget/parameter" { param_name=ledger; type=string; }
	chart_months "widget/parameter" { param_name=months; type=integer; default=runclient(:months:value); }
	chart_fund "widget/parameter" { param_name=fund; type=string; default=runclient(:fund:value); }

	sql = "	declare object info;
		declare collection gldata scope application;

		delete from collection gldata;
	
		select
		    :info:GLExpLow = :GLExpLow,
		    :info:GLExpHigh = :GLExpHigh,
		    :info:GLAssetLow = :GLAssetLow,
		    :info:GLAssetHigh = :GLAssetHigh,
		    :info:GLLiabLow = :GLLiabLow,
		    :info:GLLiabHigh = :GLLiabHigh,
		    :info:GLRsrvThres = :GLRsrvThres
		from
		    object expression('/apps/kardia/modules/gl/accounting_config.qyp/' + :parameters:ledger)
		;

		select
		    :info:endperiod = :a_period,
		    :info:endstartdate = :a_start_date
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows
		where
		    :a_ledger_number = :parameters:ledger and
		    :a_summary_only = 0 and
		    :a_start_date <= getdate()
		order by
		    :a_end_date desc
		limit
		    1
		;

		select
		    :info:startperiod = :a_period,
		    :info:startparent = :a_parent_period
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows
		where
		    :a_ledger_number = :parameters:ledger and
		    :a_summary_only = 0 and
		    :a_start_date > dateadd(month, 0 - :parameters:months, getdate())
		order by
		    :a_end_date asc
		limit
		    1
		;

		select
		    :info:dataperiod = :a_period,
		    :info:datastartdate = :a_start_date,
		    :info:n_periods = datediff(month, :a_start_date, :info:endstartdate) + 1
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows
		where
		    :a_ledger_number = :parameters:ledger and
		    :a_summary_only = 0 and
		    :a_parent_period = :info:startparent
		order by
		    :a_end_date asc
		limit
		    1
		;

		select
		    month = dateformat(:a_start_date, 'MMM\\'yy'),
		    year = datepart(year, :a_start_date),
		    NCA = condition(convert(integer, round(first(condition(:sc:s_stat_group = 'NCA', :sc:s_money_value, null)),0)) > convert(integer, round(first(condition(:sc:s_stat_group = 'THRES', :sc:s_money_value, null)),0)), convert(integer, round(first(condition(:sc:s_stat_group = 'NCA', :sc:s_money_value, null)),0)) - convert(integer, round(first(condition(:sc:s_stat_group = 'THRES', :sc:s_money_value, null)),0)), 0),
		    Reserve = condition(convert(integer, round(first(condition(:sc:s_stat_group = 'NCA', :sc:s_money_value, null)),0)) > convert(integer, round(first(condition(:sc:s_stat_group = 'THRES', :sc:s_money_value, null)),0)), convert(integer, round(first(condition(:sc:s_stat_group = 'THRES', :sc:s_money_value, null)),0)), convert(integer, round(first(condition(:sc:s_stat_group = 'NCA', :sc:s_money_value, null)),0)))
		from
		    /apps/kardia/data/Kardia_DB/a_period/rows p,
		    /apps/kardia/data/Kardia_DB/s_stats_cache/rows sc
		where
		    :p:a_ledger_number = :parameters:ledger and
		    (:p:a_period >= :info:startperiod) and
		    (:p:a_period <= :info:endperiod) and
		    :p:a_summary_only = 0 and
		    :sc:s_stat_type = 'GLREVEXP' and
		    :sc:s_stat = rtrim(:p:a_ledger_number) + '*' + rtrim(:p:a_period)
		group by
		    :p:a_period
		";
	readahead=30;
	replicasize=30;
	//autoquery=onload;

	chart_link "widget/rule"
	    {
	    ruletype=osrc_relationship;
	    target=ledger_osrc;
	    target_key_1=Ledger;
	    key_1=ledger;
	    revealed_only=yes;
	    }

	chart "widget/chart"
	    {
	    x=0;
	    y=0;
	    width=257;
	    height=135;
	    fl_width=10;
	    fl_height=10;
	    chart_type="bar";
	    title=runserver(:this:chart_title);
	    titlecolor="#334466";
	    //legend_position="right";
	    start_at_zero=yes;
	    title_size=16;
	    stacked=1;

	    series2 "widget/chart-series"
		{
		color=runserver(:this:bar_color);
		y_column="Reserve";
		//chart_type="line";
		}
	    
	    series1 "widget/chart-series"
		{
		color=runserver(:this:line_color);
		y_column="NCA";
		label="Above Reserve";
		}
	    
	    y_axis "widget/chart-axis" 
		{
		axis="y";
		label="Net Current Assets";
		}

	    x_axis "widget/chart-axis" 
		{
		condition=0;
		label="Month";
		}
	    }
	}
    }
